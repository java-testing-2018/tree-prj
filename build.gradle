
ext {
    slf4jVersion = '1.7.25'

    libraries = [
            test   : [
                    'junit:junit:4.12',
                    'org.hamcrest:hamcrest-all:1.3',

                    'org.mockito:mockito-core:2.8.47',
                    'org.springframework.boot:spring-boot-starter-test'
            ],
            log    : [
                    "org.slf4j:slf4j-api:$slf4jVersion",
                    "org.slf4j:jcl-over-slf4j:$slf4jVersion",
            ],
            commons: [
                    'com.google.guava:guava:19.0',
                    'org.apache.commons:commons-lang3:3.6',
                    'commons-io:commons-io:2.5'
            ],
            json   : [
                    'com.fasterxml.jackson.core:jackson-databind:2.9.0'
            ]
    ]
}


buildscript {
    ext {
        springBootVersion = '1.5.9.RELEASE'
    }

    repositories {
        mavenCentral()
        maven { url "https://repo.spring.io/snapshot" }
        maven { url "https://repo.spring.io/milestone" }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath("se.transmode.gradle:gradle-docker:1.2")
    }
}

allprojects {
    group = 'com.tree.springcloud'
    version = '0.0.1-SNAPSHOT'
}



subprojects {
    ext.springBootVersion = '1.5.6.RELEASE' 
    ext.springCloudVersion = "Edgware.RELEASE"

    apply plugin: 'java'
    apply plugin: 'docker'
    apply plugin: "org.springframework.boot"
    apply plugin: "jacoco"

    sourceCompatibility = 1.8
    targetCompatibility = 1.8


    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
        maven { url "https://repo.spring.io/snapshot" }
        maven { url "https://repo.spring.io/milestone" }
    }


    dependencyManagement {
        imports {
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
        }
    }

    dependencies {
        compile("org.springframework.boot:spring-boot-starter-web") {
            exclude module: "spring-boot-starter-tomcat"
        }
        compile("org.springframework.boot:spring-boot-starter-jetty")
        compile("org.springframework.boot:spring-boot-starter-actuator")

        testCompile libraries.test
    }

    task buildDocker(type: Docker, dependsOn: build) {
        applicationName = jar.baseName
        dockerfile = file('src/main/docker/Dockerfile')
        tag = "$rootProject.name/${jar.baseName.toLowerCase()}"
        doFirst {
            copy {
                from jar
                into stageDir
            }
        }
    }

    jar {
        archiveName = "${jar.baseName}.${extension}"
    }

    test {
        finalizedBy jacocoTestReport
    }

    jacocoTestReport {
        reports {
            xml.enabled false
            csv.enabled false
            html.destination "$buildDir/jacoco/report"
        }
    }

}

task wrapper(type: Wrapper) {
    gradleVersion = '4.4'
}


